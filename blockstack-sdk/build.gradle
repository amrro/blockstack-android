plugins {
    id 'org.ajoberstar.git-publish' version '2.1.0'
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'

group = 'com.github.blockstack'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 2
        versionName "0.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

    }

    signingConfigs {
        debug {
            storeFile mySigningConfigs.debug.storeFile
            storePassword  mySigningConfigs.debug.storePassword
            keyAlias mySigningConfigs.debug.keyAlias
            keyPassword mySigningConfigs.debug.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/kotlinx-io.kotlin_module'
        exclude 'META-INF/atomicfu.kotlin_module'
    }
}

dokka {
    includes = ['module.md']
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

gitPublish {
    repoUri = 'git@github.com:blockstack/blockstack-android.git'
    branch = 'gh-pages'
    contents {
        from 'build/javadoc/blockstack-sdk'
    }
    commitMessage = '[skip ci] udpateing docs' // defaults to 'Generated by gradle-git-publish'
}

dependencies {
    implementation project(':lib')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.eclipsesource.j2v8:j2v8:4.8.4@aar'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'


    implementation "com.github.walleth.kethereum:model:$kethereum_version"
    implementation "com.github.walleth.kethereum:crypto:$kethereum_version"
    implementation "com.github.walleth.kethereum:crypto_api:$kethereum_version"
    implementation "com.github.walleth.kethereum:bip32:$kethereum_version"
    implementation "com.github.walleth.kethereum:bip39:$kethereum_version"
    implementation "com.github.walleth.kethereum:bip39_wordlist_en:$kethereum_version"
    implementation "com.github.walleth.kethereum:bip44:$kethereum_version"
    implementation "com.github.walleth.kethereum:wallet:$kethereum_version"
    implementation "com.github.walleth.kethereum:keystore:$kethereum_version"
    implementation "com.github.walleth.kethereum:crypto_impl_bouncycastle:$kethereum_version"
    implementation "com.github.walleth.kethereum:extensions:$kethereum_version"

    implementation 'org.bouncycastle:bcprov-jdk15on:1.62'
    implementation "com.github.komputing:KHex:$khex_version"
    api 'com.squareup.okhttp3:okhttp:3.12.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20180130'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


task downloadBlockstackJs(type: Download) {
    src 'https://unpkg.com/blockstack@19.2.1/dist/blockstack.js'
    dest new File(buildDir, 'blockstack.js')
}

task updateBlockstackJs(dependsOn: downloadBlockstackJs, type: Copy) {
    from downloadBlockstackJs.dest
    into 'src/main/res/raw'
    include ('blockstack.js')
    rename ('(.*)', 'org_blockstack_$1')
    eachFile { fcp ->
        fcp.path = fcp.name
    }
}


