plugins {
    id 'org.ajoberstar.git-publish' version '2.1.0'
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'

group = 'com.github.blockstack'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 2
        versionName "0.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile mySigningConfigs.debug.storeFile
            storePassword  mySigningConfigs.debug.storePassword
            keyAlias mySigningConfigs.debug.keyAlias
            keyPassword mySigningConfigs.debug.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/*'
    }

    lintOptions {
        disable 'InvalidPackage'
    }
}

dokka {
    includes = ['module.md']
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

gitPublish {
    repoUri = 'git@github.com:blockstack/blockstack-android.git'
    branch = 'gh-pages'
    contents {
        from 'build/javadoc/blockstack-sdk'
    }
    commitMessage = '[skip ci] udpateing docs' // defaults to 'Generated by gradle-git-publish'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.browser:browser:1.0.0'
    api 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    implementation "com.github.komputing.kethereum:crypto_api:$kethereum_version"
    implementation "com.github.komputing.kethereum:base58:$kethereum_version"
    implementation "com.github.komputing.kethereum:hashes:$kethereum_version"
    api ("com.github.komputing.kethereum:bip32:$kethereum_version") {
        exclude group:'com.github.komputing.KHash'
    }
    api "com.github.komputing.kethereum:bip44:$kethereum_version"
    api ("com.github.komputing.kethereum:bip39:$kethereum_version") {
        exclude group:'com.github.komputing.KHash'
    }
    implementation "com.github.komputing.kethereum:bip39_wordlist_en:$kethereum_version"

    implementation 'org.bouncycastle:bcprov-jdk15on:1.62'
    api ("com.github.uport-project.kotlin-did-jwt:jwt:$did_jwt_version") {
        exclude group:'com.github.komputing.KHash'
        exclude group: 'com.squareup.okhttp3'
    }

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20180130'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


/**
 * How to get a new `blockstack.js` version?
 *
 * 1. change the url in downloadBlockstackJs
 * 2. ./gradlew updateBlockstackJs
 */


task downloadBlockstackJs(type: Download) {
    src 'https://unpkg.com/blockstack@19.4.0-beta.1/dist/blockstack.js'
    dest new File(buildDir, 'blockstack.js')
}

task updateBlockstackJs(dependsOn: downloadBlockstackJs, type: Copy) {
    from downloadBlockstackJs.dest
    into 'src/main/res/raw'
    include ('blockstack.js')
    rename ('(.*)', 'org_blockstack_$1')
    eachFile { fcp ->
        fcp.path = fcp.name
    }
}


